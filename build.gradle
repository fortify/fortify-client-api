plugins {
	id 'maven-publish'
	id 'signing'
	id 'base'
	id 'io.freefair.lombok' version '8.11' apply false
	id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

description = 'Utility classes for working with the REST API interfaces provided by the various Fortify products'

// Returns true for -PsomeProperty or -PsomeProperty=true, false if undefined or other value
ext.isPropertyTrue = { p -> project.properties[p]=='' || project.properties[p]=='true' } 

ext {
	// version.txt is generated by GitHub release-please-action, and contains 
	// the version number of the latest release version.
	versionFile = rootProject.file('version.txt')
	latestReleaseVersion = versionFile.exists() ? versionFile.text.trim() : '0.1.0'
	// Add version suffix depending on whether we are building a release or not.
	// According to Gradle conventions, SP is a successor of a release, i.e:
	//    1.0.0.RELEASE < 1.0.0.SP-SNAPSHOT < 1.0.1.RELEASE
	versionSuffix = isPropertyTrue('isReleaseVersion') ? '.RELEASE' : '.SP-SNAPSHOT'
}
version = latestReleaseVersion+versionSuffix

nexusPublishing {
	repositories {
		OSSRH {
			nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
			snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
		}
	}
}

allprojects {
	apply plugin: 'signing'
	apply plugin: 'maven-publish'
	
	group = 'com.fortify.client.api'
	version = rootProject.version
	
	println "Configuring ${project.group}:${project.name}:${project.version}"
	
	publishing {
		publications {
			mavenJava(MavenPublication) {
				pom {
					afterEvaluate {
						groupId = project.group
						artifactId = project.name
						name = project.name
						version = project.version
						description = project.description
					}
					url = 'https://github.com/fortify-ps/fortify-client-api'
					licenses {
						license {
							name = 'MIT License'
							url = 'https://opensource.org/licenses/MIT'
						}
					}
					developers {
						developer {
							id = 'rsenden'
							name = 'Ruud Senden'
							email = 'ruud.senden@microfocus.com'
							organization = 'Micro Focus Fortify'
							organizationUrl = 'https://www.microfocus.com/en-us/cyberres/application-security'
						}
					}
					scm {
						connection = "scm:git:https://github.com/fortify-ps/${rootProject.name}.git"
						developerConnection = "scm:git:https://github.com/fortify-ps/${rootProject.name}.git"
						url = "https://github.com/fortify-ps/${rootProject.name}"
					}
				}
			}
		}
	}
	// Publish to Maven local repository when publish task is executed
	publish.finalizedBy publishToMavenLocal
	
	// Sign using ORG_GRADLE_PROJECT_signingKey and ORG_GRADLE_PROJECT_signingPassword environment variables
	signing {
		def signingKey = findProperty("signingKey")
		def signingPassword = findProperty("signingPassword")
		useInMemoryPgpKeys(signingKey, signingPassword)
		required { gradle.taskGraph.hasTask("publishToOSSRH") }
		sign publishing.publications.mavenJava
	}
}

configure(subprojects.findAll {new File(it.projectDir, "src/main").exists()}) {
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	
	dependencies {
		api platform(project(':fortify-client-api-bom'))
	}
	
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	
	repositories {
		mavenCentral()
	}
	
	test {
		useJUnitPlatform()
	}
	
	task sourcesJar(type: Jar) {
		from sourceSets.main.allJava
		archiveClassifier = 'sources'
	}
	
	task myjavadoc(type: Javadoc) {
		source = sourceSets.main.allJava
		classpath = sourceSets.main.compileClasspath
		failOnError = false
	}
	
	task javadocJar(type: Jar) {
		from myjavadoc
		archiveClassifier = 'javadoc'
	}
	
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				artifact sourcesJar
				artifact javadocJar
			}
		}
	}
}

configure(subprojects.findAll {new File(it.projectDir, "src/main/lombok").exists()}) {
	apply plugin: 'io.freefair.lombok'

	// TODO For a single-module project we don't have to modify source sets, why do we need this?
	sourceSets {
		main {
			java {
				srcDir 'src/main/lombok'
			}
		}
	}
	
	myjavadoc {
		source = delombok
	}
}